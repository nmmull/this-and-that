<?xml version="1.0" encoding="utf-8"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" lang="en" xml:lang="en">
<head>
<!-- 2025-06-27 Fri 17:40 -->
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" />
<meta name="viewport" content="width=device-width, initial-scale=1" />
<title>Explosives and Distinguishers</title>
<meta name="author" content="Nathan" />
<meta name="generator" content="Org Mode" />
<link rel="stylesheet" type="text/css" href="myStyle.css" />
</head>
<body>
<div id="org-div-home-and-up"><a href="index.html">↩</a></div><div id="content" class="content">
<h1 class="title">Explosives and Distinguishers</h1>
<div id="table-of-contents" role="doc-toc">
<h2>Table of Contents</h2>
<div id="text-table-of-contents" role="doc-toc">
<ul>
<li><a href="#orgff7f493">Distinguishers</a></li>
<li><a href="#org0852c50">Inequality</a></li>
<li><a href="#org3d1a64d">Decidability</a></li>
</ul>
</div>
</div>
<p>
<i><b>Abstract.</b> TODO</i> Testing
</p>

<p>
This piece is contains quite a few (read: far too many) proofs in <a href="https://docs.racket-lang.org/pie/index.html">Pie</a>,
a simple, beautiful, but verbose dependently typed programming
language.<sup><a id="fnr.1" class="footref" href="#fn.1" role="doc-backlink">1</a></sup> Before getting started, a small logical interface
used throughout to make proofs more readable:
</p>

<div class="org-src-container">
<pre class="src src-lisp">#lang pie

(claim &#8869; U)
(define &#8869; Absurd)

(claim &#8868; U)
(define &#8868; Trivial)

(claim &#172; (&#8594; U U))
(define &#172; (&#955; (A) (&#8594; A &#8869;)))

(claim &#8743; (&#8594; U U U))
(define &#8743; (&#955; (A B) (Pair A B)))

(claim &#8744; (&#8594; U U U))
(define &#8744; (&#955; (A B) (Either A B)))

(claim &#8596; (&#8594; U U U))
(define &#8596; (&#955; (A B) (&#8743; (&#8594; A B) (&#8594; B A))))

(claim &#8800; (&#928; ((A U)) (&#8594; A A U)))
(define &#8800; (&#955; (A x y) (&#172; (= A x y))))
</pre>
</div>

<p>
I recently spent an inordinate amount of time thinking about Pie
formalizations of the following theorem:
</p>

<blockquote>
<p>
<i>1 = 0 implies I am the Pope.</i>
</p>
</blockquote>

<p>
I was interested primarily in proofs that could be carried out in the
minimal fragment of Pie, i.e., without the use of <a href="https://docs.racket-lang.org/pie/index.html#%28def._%28%28lib._pie%2Fmain..rkt%29._ind-.Absurd%29%29"><code>ind-Absurd</code></a>, the
induction principle for the empty type which expresses the <a href="https://en.wikipedia.org/wiki/Principle_of_explosion">principle
of explosion</a>.
</p>

<p>
Working in this vein leads one naturally to a standard fact of
constructive mathematics: 1 = 0 is <i>explosive</i> in the sense that
anything follows from it in the minimal fragment of Pie.<sup><a id="fnr.2" class="footref" href="#fn.2" role="doc-backlink">2</a></sup>
</p>

<div class="org-src-container">
<pre class="src src-lisp">(claim explode-1=0
  (&#8594; (= Nat 1 0) (&#928; ((A U)) A)))
(define explode-1=0
  (&#955; (1=0 A)
     (replace
       1=0
       (&#955; (a) (which-Nat a A (&#955; (_) &#8868;)))
       sole)))
</pre>
</div>

<p>
This leads one <i>further</i> to another natural enough question: <i>which
equalities are explosive?</i>
</p>
<div id="outline-container-orgff7f493" class="outline-2">
<h2 id="orgff7f493">Distinguishers</h2>
<div class="outline-text-2" id="text-orgff7f493">
<p>
Its straightforward enought to generalize the above proof to any fixed
pair of natural numbers.
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim sub1 (→ Nat Nat))
(define sub1
  (λ (n)
     (which-Nat n 0 (λ (n-1) n-1))))

(claim ∸ (→ Nat Nat Nat))
(define ∸
  (λ (m n)
     (iter-Nat n m sub1)))

(claim 5=7→1=0
  (→ (= Nat 5 7) (= Nat 1 0)))
(define 5=7→1=0
  (λ (5=7)
     (symm
       (cong
         5=7
         (the
           (→ Nat Nat)
           (λ (n) (∸ n 6)))))))

(claim explode-5=7
  (→ (= Nat 5 7) (Π ((A U)) A)))
(define explode-5=7
  (λ (5=7)
     (explode-1=0 (5=7→1=0 5=7))))
</pre>
</div>

<p>
It also works for the equality true = false, given the standard sum
type encoding of Booleans.
</p>

<div class="org-src-container">
<pre class="src src-lisp">(claim Bool U)
(define Bool (Either &#8868; &#8868;))

(claim true Bool)
(define true (left sole))

(claim false Bool)
(define false (right sole))

(claim ind-Bool
  (&#928; ((target Bool)
      (motive (&#8594; Bool U))
      (true-case (motive true))
      (false-case (motive false)))
     (motive target)))
(define ind-Bool
  (&#955; (target motive true-case false-case)
     (ind-Either
       target
       motive
       (&#955; (_) true-case)
       (&#955; (_) false-case))))

(claim bool&#8594;nat (&#8594; Bool Nat))
(define bool&#8594;nat
  (&#955; (b) (ind-Bool b (&#955; (_) Nat) 1 0)))

(claim true=false&#8594;1=0
  (&#8594; (= Bool true false) (= Nat 1 0)))
(define true=false&#8594;1=0
  (&#955; (true=false) (cong true=false bool&#8594;nat)))

(claim explode-true=false
  (&#8594; (= Bool true false) (&#928; ((A U)) A)))
(define explode-true=false
  (&#955; (true=false)
     (explode-1=0 (true=false&#8594;1=0 true=false))))
</pre>
</div>

<p>
All we're really doing here is bootstrapping; if an equality implies
that 1 = 0 then it's explosive.  This is not so natural a condition,
but it hints at a definition: let's say boolean function 𝑓 : 𝐴 → Bool
<i>distinguishes</i> two values 𝑎 and 𝑏 if 𝑓 𝑎 is true and 𝑓 𝑏 is false.
Furthermore, 𝑎 and 𝑏 are <i>distinguishable</i>, written 𝑎 ◇ 𝑏, if there
is a distinguisher for them.
</p>

<div class="org-src-container">
<pre class="src src-lisp">(claim Distinguishes
  (&#928; ((A U)
      (f (&#8594; A Bool)))
     (&#8594; A A U)))
(define Distinguishes
  (&#955; (_ f x y)
     (&#8743; (= Bool (f x) true)
        (= Bool (f y) false))))

(claim &#9671;
  (&#928; ((A U))
     (&#8594; A A U)))
(define &#9671;
  (&#955; (A x y)
     (&#931; ((f (&#8594; A Bool)))
        (Distinguishes A f x y))))

<span style="color: #b22222;">; </span><span style="color: #b22222;">the following are getters for &#9671;</span>

(claim distinguisher-&#9671;
  (&#928; ((A U)
      (x A)
      (y A)
      (x&#9671;y (&#9671; A x y)))
     (&#8594; A Bool)))
(define distinguisher-&#9671;
  (&#955; (_ _ _ x&#9671;y)
     (car x&#9671;y)))

(claim distinguishes-fst
  (&#928; ((A U)
      (x A)
      (y A)
      (x&#9671;y (&#9671; A x y)))
     (= Bool
        (distinguisher-&#9671; A x y x&#9671;y x)
        true)))
(define distinguishes-fst
  (&#955; (_ _ _ x&#9671;y)
     (car (cdr x&#9671;y))))

(claim distinguishes-snd
  (&#928; ((A U)
      (x A)
      (y A)
      (x&#9671;y (&#9671; A x y)))
     (= Bool
        (distinguisher-&#9671; A x y x&#9671;y y)
        false)))
(define distinguishes-snd
  (&#955; (_ _ _ x&#9671;y)
     (cdr (cdr x&#9671;y))))
</pre>
</div>

<p>
Distinguishability captures more generally what's going on in the case
of 5 = 7 and true = false.
</p>

<div class="org-src-container">
<pre class="src src-lisp">(claim &#9671;&#8594;explode&#8316;
  (&#928; ((A U)
      (x A)
      (y A))
     (&#8594; (&#9671; A x y)
        (= A x y)
        (&#928; ((B U)) B))))
(define &#9671;&#8594;explode&#8316;
  (&#955; (A x y x&#9671;y x=y)
    (explode-true=false
      (trans
        <span style="color: #b22222;">; true = f x</span>
        (symm (distinguishes-fst A x y x&#9671;y))
        (trans
          <span style="color: #b22222;">; f x = f y</span>
          (cong x=y (distinguisher-&#9671; A x y x&#9671;y))
          <span style="color: #b22222;">; f y = false</span>
          (distinguishes-snd A x y x&#9671;y))))))
</pre>
</div>

<p>
In other words, <i>any equality of distinguishable terms is explosive.</i>
</p>
</div>
</div>
<div id="outline-container-org0852c50" class="outline-2">
<h2 id="org0852c50">Inequality</h2>
<div class="outline-text-2" id="text-org0852c50">
<p>
Distinguishability is a strictly stronger relation than inquality in
the minimal fragment of Pie.<sup><a id="fnr.3" class="footref" href="#fn.3" role="doc-backlink">3</a></sup>
</p>

<div class="org-src-container">
<pre class="src src-lisp">(claim &#9671;&#8594;&#8800;
  (&#928; ((A U)
      (x A)
      (y A))
     (&#8594; (&#9671; A x y)
        (&#8800; A x y))))
(define &#9671;&#8594;&#8800;
  (&#955; (A x y x&#9671;y x=y)
     (&#9671;&#8594;explode&#8316; A x y x&#9671;y x=y &#8869;)))

(claim &#10216;&#8800;&#8594;&#9671;&#10217;&#8594;ind-&#8869;
  (&#8594; (&#928; ((A U)
         (x A)
         (y A))
        (&#8594; (&#8800; A x y)
           (&#9671; A x y)))
     (&#8594; &#8869; &#928; ((A U)) A))))
(define &#10216;&#8800;&#8594;&#9671;&#10217;&#8594;ind-&#8869;
  (&#955; (&#8800;&#8594;&#9671; absurd)
     (&#9671;&#8594;explode&#8316;
       &#8868;
       sole
       sole
       (&#8800;&#8594;&#9671;
         &#8868;
         sole
         sole
         (&#955; (_) absurd))
       (same sole))))
</pre>
</div>

<p>
We also can't prove ≠→◇ full Pie but this isn't something that can be
witnessed within Pie itself, as it's a theorem in the classical
extension of Pie (more on this later). The principle ≠→◇ has the
flavor of many well-studied semi-classical principles: it expresses
the ability to extract something constructive from a negation
proof.<sup><a id="fnr.4" class="footref" href="#fn.4" role="doc-backlink">4</a></sup>
</p>

<p>
And we are led quite naturally to the real motivating question of this
piece: <i>For what types are unequal values distinguishable?</i> And how
does the answer change depending on our system?
</p>

<p>
What I aim to demonstrate is that in all cases (minimal, constructive,
paraconsistent, classical) the following holds:
</p>

<blockquote>
<p>
<i>If A admits decidable equality, then unqual values of type A are
distinguishable.</i>
</p>
</blockquote>

<p>
What is most interesting is that the statement is sensitive to exactly
how decidable equality is defined.
</p>
</div>
</div>
<div id="outline-container-org3d1a64d" class="outline-2">
<h2 id="org3d1a64d">Decidability</h2>
<div class="outline-text-2" id="text-org3d1a64d">
<p>
A type 𝐴 admits decidable equality if for any two values 𝑎₁ and 𝑎₂ of
type 𝐴, we have 𝑎₁ = 𝑎₂ or 𝑎₁ ≠ 𝑎₂.  This can be seen as a restriction
of the law of excluded middle.  But the notion of decidability
originates in computability theory, where a predicate 𝑃 is decidable
if there is a computable function 𝑓 such that 𝑓(𝑥) = 1 if and only if
𝑃(x) holds.  Through the Curry-Howard lens, these two concepts
correspond; a term 𝑓 of type
</p>

<blockquote>
<p>
<code>(Π ((x A) (y A)) (∨ (= 𝐴 𝑎₁ 𝑎₂) (≠ 𝐴 𝑎₁ 𝑎₁)))</code>
</p>
</blockquote>

<p>
is a computation where <code>(f x y)</code> evaluates to <code>(left _)</code> if x = y and
<code>(right _)</code> otherwise.  We begin by formalizing this, but with a
twist: we'll work in the minimal fragment of Pie in order to
demonstrate that <i>this correspondence depends on the principle of
explosion</i>.
</p>

<p>
A type 𝐴 (viewed through Curry-Howard lense) is <b>decidable</b> if either
𝐴 or ¬𝐴 holds.  The law of excluded middle is equivalent to every type
being decidable.  We can use this baseline notion of decidability to
define decidability of predicates, up to decidable equality.  Note
that if we want to use binary predicates in their Curried form, we
need to uncurry them to express their decidability.
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim Decᴾ
  (Π ((A U)
      (P (→ A U)))
     U))
(define Decᴾ
  (λ (A P)
     (Π ((x A)) (Dec (P x)))))

(claim Uncurryᴾ
  (Π ((A U)
      (B U)
      (P (→ A B U)))
     (→ (Pair A B) U)))
(define Uncurryᴾ
  (λ (_ _ P p)
     (P (car p) (cdr p))))

(claim Decᴾ²
  (Π ((A U)
      (B U)
      (P (→ A B U)))
     U))
(define Decᴾ²
  (λ (A B P)
     (Decᴾ (Pair A B) (Uncurryᴾ A B P))))

(claim Dec⁼ (→ U U))
(define Dec⁼
  (λ (A)
     (Decᴾ²
       A
       A
       (λ (x y) (= A x y)))))
</pre>
</div>

<p>
The computability theoretic form of decidability can be expressed as
having a decider, i.e., the existence of a function which witnesses
the truth value of the predicate (we'll use <code>true</code> and <code>false</code> instead
of <code>1</code> and <code>0</code>).
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim Decides
  (Π ((A U)
      (f (→ A Bool))
      (P (→ A U)))
     U))
(define Decides
  (λ (A f P)
     (Π ((x A))
        (↔ (P x)
           (= Bool (f x) true)))))

(claim Decider
  (Π ((A U)
      (P (→ A U)))
     U))
(define Decider
  (λ (A P)
     (Σ ((f (→ A Bool)))
        (Decides A f P))))

(claim uncurry
  (Π ((A U)
      (B U)
      (C U))
     (→ (→ A B C)
        (→ (Pair A B) C))))
(define uncurry
  (λ (_ _ _ f p)
     (f (car p) (cdr p))))

(claim Decides₂
  (Π ((A U)
      (B U)
      (f (→ A B Bool))
      (P (→ A B U)))
     U))
(define Decides₂
  (λ (A B f P)
     (Decides
       (Pair A B)
       (uncurry A B Bool f)
       (Uncurryᴾ A B P))))

(claim Decider₂
  (Π ((A U)
      (B U)
      (P (→ A B U)))
     U))
(define Decider₂
  (λ (A B P)
     (Σ ((f (→ A B Bool)))
        (Decides₂ A B f P))))

(claim Decides⁼
  (Π ((A U)
      (f (→ A A Bool)))
     U))
(define Decides⁼
  (λ (A f)
     (Decides₂
       A
       A
       f
       (λ (x y) (= A x y)))))

(claim Decider⁼ (→ U U))
(define Decider⁼
  (λ (A)
     (Σ ((f (→ A A Bool)))
        (Decides⁼ A f))))
</pre>
</div>

<p>
The easy direction: if a predicate 𝑃 has a decider, then it is
decidable.  The proof: given 𝑥, ask the decider if 𝑃(𝑥) or ¬𝑃(𝑥).  We
break the proof up into two parts because working with Σ-types in Pie
is <i>not</i> ergonomic.
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim decides→decᴾ-helper
  (Π ((A U)
      (f (→ A Bool))
      (P (→ A U))
      (x A))
     (→ (↔ (P x) (= Bool (f x) true))
        (Dec (P x)))))
(define decides→decᴾ-helper
  (λ (_ f P x)
     (ind-Bool
       (f x)
       (λ (b)
          (→ (↔ (P x) (= Bool b true))
             (Dec (P x))))
       (λ (prf)
          (left ((cdr prf) (same true))))
       (λ (prf)
          (right
            (λ (px)
               (true≠false
                 (symm ((car prf) px)))))))))

(claim decider→decᴾ
  (Π ((A U)
      (P (→ A U)))
     (→ (Decider A P)
        (Decᴾ A P))))
(define decider→decᴾ
  (λ (A P decider x)
     (decides→decᴾ-helper
       A
       (car decider)
       P
       x
       ((cdr decider) x))))
</pre>
</div>

<p>
The hard direction (or, rather, the impossible direction in the
minimal setting): if 𝑃 is decidable then it has a decider.  Now, we
can't <i>exactly</i> show that this does not hold in minimal Pie; proving
things don't exist in a constructive setting is tricky.  But what
we're really after is the following: if 𝑃 is decidable, then the
<i>obvious</i> decider for 𝑃 is, in fact, a decider.
</p>

<p>
The obvious decider is the one which is encoded in the proof of
decidability. We can define the decider generally over decidable
predicates (given a proof of decidability).
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim left?
  (Π ((A U)
      (B U))
     (→ (Either A B)
        Bool)))
(define left?
  (λ (_ _ e)
     (ind-Either
       e
       (λ (_) Bool)
       (λ (_) true)
       (λ (_) false))))

(claim ?
  (Π ((A U)
      (P (→ A U)))
     (→ (Decᴾ A P) A Bool)))
(define ?
  (λ (_ P decᴾ x)
     (left?
       (P x)
       (¬ (P x))
       (decᴾ x))))

(claim ?₂
  (Π ((A U)
      (B U)
      (P (→ A B U))
      (decᴾ² (Decᴾ² A B P)))
     (→ A B Bool)))
(define ?₂
  (λ (A B P decᴾ² x y)
     (?
       (Pair A B)
       (Uncurryᴾ A B P)
       decᴾ²
       (cons x y))))

(claim ≟
  (Π ((A U))
     (→ (Dec⁼ A) A A Bool)))
(define ≟
  (λ (A) (?₂ A A (λ (x y) (= A x y)))))
</pre>
</div>

<p>
Within the minimal fragment of Pie, if we know the value of this
decider on 𝑎, then we can derive proofs of the 𝑃(𝑎) or ¬𝑃(𝑎),
depending on that value.
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim p?=true→p
  (Π ((A U)
      (P (→ A U))
      (decᴾ (Decᴾ A P))
      (x A))
     (→ (= Bool (? A P decᴾ x) true)
        (P x))))
(define p?=true→p
  (λ (_ P decᴾ x)
     (ind-Either
       (decᴾ x)
       (λ (e)
          (→ (= Bool
                (left? (P x) (¬ (P x)) e)
                true)
             (P x)))
       (λ (px _) px)
       (λ (_ false=true)
          (explode-true=false
            (symm false=true)
            (P x))))))

(claim p?=false→¬p
  (Π ((A U)
      (P (→ A U))
      (decᴾ (Decᴾ A P))
      (x A))
     (→ (= Bool
           (? A P decᴾ x)
           false)
        (¬ (P x)))))
(define p?=false→¬p
  (λ (_ P decᴾ x)
     (ind-Either
       (decᴾ x)
       (λ (e)
          (→ (= Bool
                (left? (P x) (¬ (P x)) e)
                false)
             (¬ (P x))))
       (λ (_ true=false)
          (explode-true=false
            true=false
            (¬ (P x))))
       (λ (¬px _) ¬px))))
</pre>
</div>

<p>
What we <i>can't</i> do within minimal Pie is determine the value of the
decider itself given 𝑃(𝑎) or ¬𝑃(𝑎).  For example, 𝑃(𝑎) implies the
equality (? 𝑃 𝑎) = true given the principle of explosion, but this
implication also <i>implies</i> the principle of explosion.  This
ultimately comes down to the fact that there isn't much we can do in
the ¬𝑃(𝑎) branch of the decidability of 𝑃 except explode it with our
assumption 𝑃(𝑎).
</p>

<div class="org-src-container">
<pre class="src src-lisp">(claim ind-&#8869;&#8594;p&#8594;p?=true
  (&#8594; (&#8594; &#8869; (&#928; ((A U)) A))
     (&#928; ((A U)
         (P (&#8594; A U))
         (dec&#7486; (Dec&#7486; A P))
         (x A))
        (&#8594; (P x)
           (= Bool
              (? A P dec&#7486; x)
              true)))))
(define ind-&#8869;&#8594;p&#8594;p?=true
  (&#955; (ind-&#8869; _ P dec&#7486; x px)
     (ind-Either
       (dec&#7486; x)
       (&#955; (e)
          (= Bool
             (left? (P x) (&#172; (P x)) e)
             true))
       (&#955; (_) (same true))
       (&#955; (&#172;px)
          (ind-&#8869;&#8594;explode
            ind-&#8869;
            (P x)
            &#172;px
            px
            (= Bool false true))))))

(claim &#10216;p&#8594;p?=true&#10217;&#8594;ind-&#8869;
  (&#8594; (&#928; ((A U)
         (P (&#8594; A U))
         (dec&#7486; (Dec&#7486; A P))
         (x A))
        (&#8594; (P x)
           (= Bool
              (? A P dec&#7486; x)
              true)))
     (&#8594; &#8869; (&#928; ((A U)) A))))
(define &#10216;p&#8594;p?=true&#10217;&#8594;ind-&#8869;
  (&#955; (p&#8594;p?=true absurd)
     (explode-true=false
       (symm
         (p&#8594;p?=true
           &#8868;
           (&#955; (_) &#8868;)
           (&#955; (_) (right (&#955; (_) absurd)))
           sole
           sole)))))

(claim ind-&#8869;&#8594;&#172;p&#8594;p?=false
  (&#8594; (&#8594; &#8869; (&#928; ((A U)) A))
     (&#928; ((A U)
         (P (&#8594; A U))
         (dec&#7486; (Dec&#7486; A P))
         (x A))
        (&#8594; (&#172; (P x))
           (= Bool
              (? A P dec&#7486; x)
              false)))))
(define ind-&#8869;&#8594;&#172;p&#8594;p?=false
  (&#955; (ind-&#8869; _ P dec&#7486; x &#172;px)
     (ind-Either
       (dec&#7486; x)
       (&#955; (e)
          (= Bool
             (left? (P x) (&#172; (P x)) e)
             false))
       (&#955; (px)
          (ind-&#8869;&#8594;explode
            ind-&#8869;
            (P x)
            &#172;px
            px
            (= Bool true false)))
       (&#955; (_) (same false)))))

(claim &#10216;&#172;p&#8594;p?=false&#10217;&#8594;ind-&#8869;
  (&#8594; (&#928; ((A U)
         (P (&#8594; A U))
         (dec&#7486; (Dec&#7486; A P))
         (x A))
        (&#8594; (&#172; (P x))
           (= Bool
              (? A P dec&#7486; x)
              false)))
     (&#8594; &#8869; (&#928; ((A U)) A))))
(define &#10216;&#172;p&#8594;p?=false&#10217;&#8594;ind-&#8869;
  (&#955; (&#172;p&#8594;p?=false absurd)
     (explode-true=false
       (&#172;p&#8594;p?=false
         &#8868;
         (&#955; (_) &#8869;)
         <span style="color: #b22222;">; artificial proof of negation</span>
         (&#955; (_) (left absurd))
         sole
         (&#955; (absurd) absurd)))))
</pre>
</div>

<p>
Another theme that comes up here: in almost all cases, the <code>Trivial</code>
type is sufficient to find a counterexample in minimal Pie of a given
principle.  Most come down to an artificial proof of a negation using
our <code>Absurd</code> assumption in the principle of explosion (see the last
example above).
</p>

<p>
All of this amounts to the fact that having a decider is a strictly
stronger notion of decidability within minimal Pie (in the sense that
we cannot prove that the obvious decider is a decider).
</p>

<div class="org-src-container">
<pre class="src src-nil">(claim ind-⊥→decᴾ→decides
  (→ (→ ⊥ (Π ((A U)) A))
     (Π ((A U)
         (P (→ A U))
         (decᴾ (Decᴾ A P)))
        (Decides A (? A P decᴾ) P))))
(define ind-⊥→decᴾ→decides
  (λ (ind-absurd A P decᴾ x)
     (cons
       (ind-⊥→p→p?=true
         ind-absurd
         A
         P
         decᴾ
         x)
       (p?=true→p
         A
         P
         decᴾ
         x))))

(claim ⟨decᴾ→decides⟩→ind-⊥
  (→ (Π ((A U)
         (P (→ A U))
         (decᴾ (Decᴾ A P)))
        (Decides A (? A P decᴾ) P))
     (→ ⊥ (Π ((A U)) A))))
(define ⟨decᴾ→decides⟩→ind-⊥
  (λ (decᴾ→decides absurd)
     (explode-true=false
       (symm
         ((car
            ((decᴾ→decides
               ⊤
               (λ (_) ⊤)
               (λ (_) (right (λ (_) absurd))))
              sole))
            sole)))))
</pre>
</div>

<p>
But even though having a decider is stronger, it's not sufficient to
prove distinguishability from inequality in minimal Pie.
</p>

<p>
By virtue of having defined everything in terms of general
decidability, we get immediately as a corollary our first result, that
decidable equality implies distinguishable unequals in classical Pie.
Note again that the antecedent here is vacuous because it a special
case of the law of excluded middle.  The distinguisher is the partial
application of the obvious decider.
</p>
</div>
</div>
<div id="footnotes">
<h2 class="footnotes">Footnotes: </h2>
<div id="text-footnotes">

<div class="footdef"><sup><a id="fn.1" class="footnum" href="#fnr.1" role="doc-backlink">1</a></sup> <div class="footpara" role="doc-footnote"><p class="footpara">TODO</p></div></div>

<div class="footdef"><sup><a id="fn.2" class="footnum" href="#fnr.2" role="doc-backlink">2</a></sup> <div class="footpara" role="doc-footnote"><p class="footpara">See
Proposition 3.2 of Constructivism in Mathematics by Trolestra and van
Dalen.</p></div></div>

<div class="footdef"><sup><a id="fn.3" class="footnum" href="#fnr.3" role="doc-backlink">3</a></sup> <div class="footpara" role="doc-footnote"><p class="footpara">Moving forward, we'll take advantage
of the fact that the principle of explosion is not a theorem in the
minimal fragment of Pie: to show that any other claim is not a
theorem, it suffices to show that it implies explosion.</p></div></div>

<div class="footdef"><sup><a id="fn.4" class="footnum" href="#fnr.4" role="doc-backlink">4</a></sup> <div class="footpara" role="doc-footnote"><p class="footpara">See TODO</p></div></div>


</div>
</div></div>
</body>
</html>
